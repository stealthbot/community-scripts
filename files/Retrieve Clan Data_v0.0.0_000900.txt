Option Explicit
'___________________________________________________________________________________________________________________________________________________

 Script("Name") = "Retrieve Clan Data"													           
 Script("Author") = "WataBoy7"															   
 Script("Version") = "v1.0" 															   
 Script("Revision") = "0"															   											 
 Script("Description") = "Displays a Clan's full name, how many players it has, and an optional list of all members and their Clan Rank."			   						   
'___________________________________________________________________________________________________________________________________________________

'_______________________
'| |  Define Colors  | |
'=======================
 Public Const vbAzure      = &HFFE199
 Public Const vbGold       = &H33F0FF
 Public Const vbTitle      = &H6699FF
 Public Const vbInfo       = &HE6E6FF
 Public Const vbLightCyan  = &HFFFFCC
 Public Const vbLightGreen = &H99FF99
 Public Const vbLightGrey  = &HF5F5F5


'_____________________
'| |  Define Vars  | |
'=====================
 Public DisplayID
 Public Show_Members
 Public Const PER_PAGE = 15

 Private webObject
 Private P_Data
 Private P_Name
 Private P_Rank
 Private Clan_Name


'____________________
'| |  Event Load  | |
'====================
 Sub Event_Load()

    If GetSettingsEntry("Script_Version") = vbNullString Then
       WriteSettingsEntry "Script_Version", Script("Version")
       AddChat vbTitle, Script("Name") & ": ", vbInfo, "Creating command(s) on First Run ... Please Wait."
       Call Create_Command("claninfo")
    End If

    Set P_Data = CreateObject("Scripting.Dictionary")
    Set webObject = CreateObject("Microsoft.XMLHTTP")

 End Sub


'_______________________
'| |  Event Command  | |
'=======================
 Sub Event_Command(Command)

    Select Case Command.Name
       Case "claninfo":		Call ClanInfo(Command, Command.Username)
    End Select

 End Sub


'____________________________
'| |  Retrieve Clan Data  | |
'============================
 Private Function Retrieve_Clan_Data(ClanTag)

    Dim web
    Dim P_Tag
    Dim P_RankNum_Raw
    Dim P_RankNum
    Dim i, j

    '// Clear Clan Data in case the function is run subsequent time(s)
    P_Data.RemoveAll

       '// Default - Runs for 10 pages. If the last page is found before this, then the loop ends there.
       For i = 1 To 10
          webObject.Open "GET", "http://classic.battle.net/war3/ladder/w3xp-clan-profile.aspx?ClanTag=" &UCase(ClanTag)& "&Gateway=" &BotVars.Gateway& "&PageNo=" &i& "", False
          webObject.Send
          web = webObject.responseText

          '// Ensures that the following action(s) only happen once: on the 1st Page
          Select Case i
             Case "1":
                Clan_Name = "<title>Frozen Throne - " & BotVars.Gateway & " - "
                If InStr(web, Clan_Name) = 0 Then
                   Retrieve_Clan_Data = False
                   Exit Function
                Else
                   Clan_Name = Split(Split(web, Clan_Name)(1), " - Clan Profile</title>")(0)
                End If
          End Select

          P_Tag = Split(web, "&PlayerName=")
          P_RankNum_Raw = Split(web, "clans/rank")

          For j = 1 to UBound(P_Tag)
             P_Name = Split(P_Tag(j), """>")(0)
             P_Name = Replace(P_Name, "%5b", "[")
             P_Name = Replace(P_Name, "%5d", "]")
             P_RankNum = Split(P_RankNum_Raw(j), ".gif")(0)

             Select Case P_RankNum
                Case "0": P_Rank = Array(vbLightGreen, "Peon")
                Case "2": P_Rank = Array(vbAzure, "Grunt")
                Case "3": P_Rank = Array(vbLightCyan, "Shaman")
                Case "4": P_Rank = Array(vbGold, "Chieftain")
             End Select

             P_Data.Item(P_Name) = P_Rank
          Next

          '// If the last page is found, exit out.
          If UBound(P_Tag) < PER_PAGE Then Exit For 
       Next
    Retrieve_Clan_Data = True
 End Function


'___________________________
'| |  Display Clan Data  | |
'===========================
 Private Sub ClanInfo(Command, User)

    Dim ClanTag
    Dim i

    If Command.IsValid And Command.HasAccess Then
       If User = BotVars.Username Then DisplayID = 4 Else DisplayID = 1 End If
       ClanTag = Channel.GetUser(User).Clan 

       If Len(Command.Args) = 0 Then
          If ClanTag = vbNullString Then
             dsp DisplayID, "Error: You do not belong to a clan!", User, vbRed
             Exit Sub
          End If
       Else
          ClanTag = Split(Command.Args)(0)
       End If

       If Retrieve_Clan_Data(ClanTag) = False Then
          dsp DisplayID, "Error: Invalid Clan Tag specified!", User, vbRed
          Exit Sub
       End If

       If DisplayID = 4 Then
          AddChat vbWhite, "Clan "& ClanTag &" ", vbGold, "["& Clan_Name &"]", vbWhite, " has ", vbGreen, P_Data.Count, vbWhite, " members."
       Else
          dsp DisplayID, "Clan "& ClanTag &" ["& Clan_Name &"] has "& P_Data.Count &" members.", User
       End If

       Show_Members = MsgBox("Display the names and ranks of individual Clan " & ClanTag & " members?", vbYesNo, Script("Name") & " by " & Script("Author"))
       If Show_Members = vbYes Then
          P_Name = P_Data.Keys
          P_Rank = P_Data.Items
          For i = 0 To P_Data.Count-1
             If DisplayID = 4 Then 
                AddChat vbLightGrey, P_Name(i), vbWhite, " is a ", P_Rank(i)(0), P_Rank(i)(1), vbWhite, "."
             Else
                dsp DisplayID, P_Name(i) & " is a " & P_Rank(i)(1) & ".", User
             End If
          Next
       End If

    End If

 End Sub


'____________________________
'| |  Create The Command  | |
'============================
 Private Sub Create_Command(Command)

    If OpenCommand(Command) Is Nothing Then
       With CreateCommand(Command)
          .Description = Script("Description") & vbNewLine & vbNewLine &_
          "Aliases: "& BotVars.Trigger &"cinfo, "& BotVars.Trigger &"rcd" & vbNewLine & vbNewLine &_
          "Syntax: " & BotVars.Trigger & "claninfo <clantag>. Leave blank to display information about your own Clan."
          .RequiredRank = "0"
	  .Aliases.Add "cinfo"
	  .Aliases.Add "rcd"
          .Save
       End With
    End If

    AddChat vbTitle, Script("Name") & ": ", vbInfo, "Command(s) successfully created!"
    AddChat vbTitle, Script("Name") & ": ", vbInfo, "Please view the Command Manager for description, syntax, and command aliases."
 End Sub